package GoldMine

import UnitObjEditing
import UnitIds
import Buildings
import ObjectIdGenerator
import ObjectIds
import ClosureEvents
import TerrainUtils
import Abilities
import Icons

import GoldMineAbility

public constant GOLD_MINE_ID = compiletime(UNIT_ID_GEN.next())

@compiletime function gen()
	new BuildingDefinition(GOLD_MINE_ID, UnitIds.farm)
		..setIconGameInterface(Icons.bTNGoldmine)
		..setName("|cffFF7B29 Gold Mine")
		..setHotkey("G")
		..setTooltipBasic("|cffFFCC00Build |cffFF7B29Gold Mine [G]")
		..setTooltipExtended("")
		..setButtonPositionX(0)
		..setButtonPositionY(0)
		..setBuildTime(2)
		..setGoldCost(20)
		..setLumberCost(150)
		..setBuildTime(5)
		..setFoodProduced(0)
		..setModelFile(Buildings.goldmine1)
		..setCollisionSize(100)
		..setPathingMap("\\PathTextures\\16x16Goldmine.tga")
		..setGroundTexture("")

function checkGoldTerrainType(vec2 pos, int inc, int modifier) returns boolean
	return pos.add((90. * modifier) * inc, 90. * inc).getTerrainType() == 1315529573

init
	EventListener.add(EVENT_PLAYER_UNIT_CONSTRUCT_FINISH) () ->
		let u = GetTriggerUnit()
		let pos = u.getPos()

		if u.getTypeId() == GOLD_MINE_ID
			int gmlvl = 0
			for i = -4 to 4
				if checkGoldTerrainType(pos, i, 1)
					gmlvl = gmlvl + 1
				if checkGoldTerrainType(pos, i, -1)
					gmlvl = gmlvl + 1

			if gmlvl > 0
				flashEffect(Abilities.pileofGold, pos, 1.2, angle(0))
				u.addAbility(GOLD_MINE_ABILITY_IDS[gmlvl])
				pos.setTerrainType(1516532850, 0, 2, 0)
