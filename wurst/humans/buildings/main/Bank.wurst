package Bank

import UnitObjEditing
import UnitIds
import ObjectIdGenerator
import ObjectIds
import ClosureTimers
import StandardTextTags
import Buildings
import Icons

import TaxBurdenIncrease
import GoldMine
import TaxLaw
import Worker

public constant BANK_ID = compiletime(UNIT_ID_GEN.next())

@compiletime function gen()
	new BuildingDefinition(BANK_ID, UnitIds.blacksmith)
		..setModelFile(Buildings.mageTower)
		..setIconGameInterface(Icons.bTNMageTower)
		..setName("|cffFF7B29 Bank")
		..setHotkey("B")
		..setTooltipBasic("|cffFFCC00Build |cffFF7B29 Bank [B]")
		..setTooltipExtended("Bank provides gold depending on number of workers")
		..setButtonPositionX(0)
		..setButtonPositionY(0)
		..setBuildTime(2)
		..setRequirements(commaList(GOLD_MINE_ID, TAX_LAW_ID))
		..setResearchesAvailable(commaList(TAX_BURDEN_INCREASE_ID))
		..setGoldCost(250)
		..setLumberCost(150)

init
	doPeriodically(10) i ->
		for i = 0 to bj_MAX_PLAYER_SLOTS - 1
			let p = players[i]
			let numOfWorkers = GetUnitsOfPlayerAndTypeId(p, WORKER_ID).size()
			let upgradeLvl = GetPlayerTechCountSimple(TAX_BURDEN_INCREASE_ID, p)
			let goldAmount = numOfWorkers * (upgradeLvl + 1)
			if numOfWorkers > 0
				let playerBanks = GetUnitsOfPlayerAndTypeId(p, BANK_ID)
				if playerBanks.size() > 0
					let bank = playerBanks.next()
					bank.queueAnimation("Walk")
					p.addGold(goldAmount)
					createGoldBountyTextTag(bank, goldAmount, players[i])